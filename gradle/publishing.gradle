buildscript {
    // Also include the eleven repositories in the buildscript block,
    // so the gradle plugins can be resolved.
    apply from: "$rootDir/gradle/eleven-repositories.gradle", to: buildscript

    dependencies {
        classpath "gradle.plugin.org.datlowe.maven-publish-auth:buildSrc:2.0.2"
    }
}

// For more info, see: https://plugins.gradle.org/plugin/org.datlowe.maven-publish-auth
apply plugin: org.hibernate.build.gradle.publish.auth.maven.AuthenticationManager

publishing {
    repositories {
        maven {
            // CAUTION: Credentials are resolved from ~/.m2/settings.xml by the 'maven-publish-auth' plugin.
            // The below name should match the name of the server id in the xml file.
            // if the publish actions fails with an '401 Unauthorized', run the publish task
            // in the terminal with the additional '--info' or '--debug' flag, to check which credentials are used.
            // The credentials are cached by the plugin so it could take some time if you make changes to them.
            name = 'nexus'

            url = version.endsWith('SNAPSHOT') ?
                    'https://nexus.eleven.nl/content/repositories/snapshots' :
                    'https://nexus.eleven.nl/content/repositories/releases'
        }
    }
}
